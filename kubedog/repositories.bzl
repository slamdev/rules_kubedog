"""Declare runtime dependencies

These are needed for local dev, and users must install them as well.
See https://docs.bazel.build/versions/main/skylark/deploying.html#dependencies
"""

load("@bazel_tools//tools/build_defs/repo:http.bzl", "http_archive")
load("@bazel_tools//tools/build_defs/repo:utils.bzl", "maybe")
load("//kubedog/private:toolchains_repo.bzl", "PLATFORMS", "toolchains_repo")

# WARNING: any changes in this function may be BREAKING CHANGES for users
# because we'll fetch a dependency which may be different from one that
# they were previously fetching later in their WORKSPACE setup, and now
# ours took precedence. Such breakages are challenging for users, so any
# changes in this function should be marked as BREAKING in the commit message
# and released only in semver majors.
def rules_kubedog_dependencies():
    # The minimal version of bazel_skylib we require
    maybe(
        http_archive,
        name = "bazel_skylib",
        sha256 = "c6966ec828da198c5d9adbaa94c05e3a1c7f21bd012a0b29ba8ddbccb2c93b0d",
        urls = [
            "https://github.com/bazelbuild/bazel-skylib/releases/download/1.1.1/bazel-skylib-1.1.1.tar.gz",
            "https://mirror.bazel.build/github.com/bazelbuild/bazel-skylib/releases/download/1.1.1/bazel-skylib-1.1.1.tar.gz",
        ],
    )

_DOC = "TODO"
_ATTRS = {
    "kubedog_version": attr.string(mandatory = True),
    "platform": attr.string(mandatory = True, values = PLATFORMS.keys()),
}

def _kubedog_repo_impl(repository_ctx):
    repository_ctx.report_progress("Downloading Kubedog releases info")
    repository_ctx.download(
        url = ["https://api.github.com/repos/werf/kubedog/releases"],
        output = "versions.json",
    )
    versions = repository_ctx.read("versions.json")
    version_found = False
    for v in json.decode(versions):
        version = v["tag_name"].lstrip("v")
        if version == repository_ctx.attr.kubedog_version:
            version_found = True
    if not version_found:
        fail("did not find {} version in https://api.github.com/repos/werf/kubedog/releases".format(repository_ctx.attr.kubedog_version))

    file_url = "https://tuf.kubedog.werf.io/targets/releases/{}/{}/bin/kubedog".format(repository_ctx.attr.kubedog_version, repository_ctx.attr.platform)

    repository_ctx.report_progress("Downloading Kubedog toolchain")
    repository_ctx.download(
        url = file_url,
        executable = True,
        output = "kubedog",
    )

    build_content = """#Generated by kubedog/repositories.bzl
load("@slamdev_rules_kubedog//kubedog:toolchain.bzl", "kubedog_toolchain")
kubedog_toolchain(name = "kubedog_toolchain", target_tool = select({
        "@bazel_tools//src/conditions:host_windows": "kubedog.exe",
        "//conditions:default": "kubedog",
    }),
)
"""

    # Base BUILD file for this repository
    repository_ctx.file("BUILD.bazel", build_content)

kubedog_repositories = repository_rule(
    _kubedog_repo_impl,
    doc = _DOC,
    attrs = _ATTRS,
)

# Wrapper macro around everything above, this is the primary API
def kubedog_register_toolchains(name, **kwargs):
    """Convenience macro for users which does typical setup.

    - create a repository for each built-in platform like "kubedog_linux_amd64" -
      this repository is lazily fetched when kubedog is needed for that platform.
    - TODO: create a convenience repository for the host platform like "kubedog_host"
    - create a repository exposing toolchains for each platform like "kubedog_platforms"
    - register a toolchain pointing at each platform
    Users can avoid this macro and do these steps themselves, if they want more control.

    Args:
        name: base name for all created repos, like "kubedog0_6_1"
        **kwargs: passed to each kubedog_repositories call
    """
    for platform in PLATFORMS.keys():
        kubedog_repositories(
            name = name + "_" + platform,
            platform = platform,
            **kwargs
        )
        native.register_toolchains("@%s_toolchains//:%s_toolchain" % (name, platform))

    toolchains_repo(
        name = name + "_toolchains",
        user_repository_name = name,
    )
